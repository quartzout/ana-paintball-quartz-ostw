import "../../Modules/match time/match_time.ostw";
import "logging.ostw";

void Loop(
    in Number delay, 
    const () => void actions, 
    const () => Boolean condition = () => true, 
    const () => Boolean stoppingCondition = () => false) {
    
    while(!stoppingCondition()) {
        if (condition()) actions();
        Wait(delay, WaitBehavior.IgnoreCondition);
    }
}

void LoopWhileConditionIsTrue(
    in Number delay, 
    const () => void actions) 
{
    actions();
    Wait(delay, WaitBehavior.IgnoreCondition);
    LoopIfConditionIsTrue();
}

# Restarts the game immediately when match could be restarted, otherwise waits until match coluld be restarted and then restarts.
# Subroutine, and should be called asynchronously.
void RestartWhenPossible() "Restart when possible" {
    MinWait(); //for some reason waitUntil doesnt work without some wait before it ¯\_(ツ)_/¯ ¯\_(ツ)_/¯ ¯\_(ツ)_/¯ (╯°□°）╯︵ ┻━┻
    WaitUntil(MatchTime > 5, 9999);
    EnableInspectorAndLog("Restarting");
    RestartMatch();
}

T CacheRef<T>(const () => T valueGetter, ref T cacheStore) {
    
    if (cacheStore == null) {
        cacheStore = valueGetter();
    }

    return cacheStore;
}

T CacheValue<T>(const () => T valueGetter, ref T cacheStore, T defaultValue) {
    
    if (cacheStore == defaultValue) {
        cacheStore = valueGetter();
    }

    return cacheStore;
}

