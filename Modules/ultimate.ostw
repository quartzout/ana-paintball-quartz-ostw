import "../Namespaces/_helpers.ostw";
import "../Namespaces/_constants.ostw";
import "../Namespaces/_shared.ostw";

playervar Number killsToUltimate = 0;
playervar Number ultimateGivenDamageModID;
playervar Number ultimateRecievedDamageModID;
rule: "Player started using ultimate"
Event.OngoingPlayer
if (EventPlayer.isActive)
if (IsUsingUltimate(EventPlayer))
{
    EventPlayer.killsToUltimate = 0;
    
    # Physics
    SetMoveSpeed(EventPlayer, 100 * ULTIMATE_MOVE_SPEED_SCALAR);
    SetGravity(EventPlayer, 100 * ULTIMATE_GRAVITY_SCALAR);
    SetJumpVerticalSpeed(EventPlayer, 100 * ULTIMATE_JUMP_VELOCITY_SCALAR);
    
    # Start Compensating Damage Mods (to keep event damage values roughly the same for combat and grenade event triggers)
    EventPlayer.ultimateGivenDamageModID = StartDamageModification(All, EventPlayer, 66.666, DamageModificationRev.ReceiversAndDamagers);
    EventPlayer.ultimateRecievedDamageModID = StartDamageModification(EventPlayer, All, 200, DamageModificationRev.ReceiversAndDamagers);
    
    # Reset Ability Cooldowns
    SetAbilityCooldown(EventPlayer, Button.Ability1, 0);
    SetAbilityCooldown(EventPlayer, Button.Ability2, 0);
}

rule: "Player stopped using ultimate"
Event.OngoingPlayer
if (EventPlayer.isActive)
if (!IsUsingUltimate(EventPlayer))
{
    ResetPlayerPhysicsSettings(EventPlayer);
    StopDamageModification(EventPlayer.ultimateGivenDamageModID);
    StopDamageModification(EventPlayer.ultimateRecievedDamageModID);
}

rule: "Add ultimate charge on player kill"
Event.OnElimination
if (EventPlayer.isActive)
if (!IsUsingUltimate(EventPlayer))
{
    EventPlayer.killsToUltimate += 1;
    SetUltimateCharge(EventPlayer, killsToUltimate / KILLS_FOR_ULTIMATE * 100);
}

rule: "Reset ultimate charge on player death"
Event.OnDeath
if (EventPlayer.isActive)
{
    EventPlayer.killsToUltimate = 0;
}

rule: "Player used Sleep in ultimate"
Event.OngoingPlayer
if (EventPlayer.isActive)
if (IsUsingUltimate(EventPlayer))
if (IsUsingAbility1(EventPlayer))
{
    Wait(1, WaitBehavior.IgnoreCondition);
    SetAbilityCooldown(EventPlayer, Button.Ability1, ULTIMATE_SLEEP_COOLDOWN);
}

rule: "Player used Grenade in ultimate"
Event.OngoingPlayer
if (EventPlayer.isActive)
if (IsUsingUltimate(EventPlayer))
if (IsUsingAbility2(EventPlayer))
{
    Wait(1, WaitBehavior.IgnoreCondition);
    SetAbilityCooldown(EventPlayer, Button.Ability2, ULTIMATE_GRENADE_COOLDOWN);
}
